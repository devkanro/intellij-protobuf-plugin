{
  generate=[names="long" psi="yes" tokens="no"]
  parserClass="io.kanro.idea.plugin.protobuf.lang.parser.ProtobufParser"
  parserUtilClass="io.kanro.idea.plugin.protobuf.lang.parser.ProtobufParserUtil"
  parserImports=[
    "static io.kanro.idea.plugin.protobuf.lang.psi.token.ProtobufTokens.*"
  ]
  psiClassPrefix="Protobuf"
  psiImplClassSuffix="Impl"
  psiPackage="io.kanro.idea.plugin.protobuf.lang.psi.proto"
  psiImplPackage="io.kanro.idea.plugin.protobuf.lang.psi.proto.impl"

  implements="io.kanro.idea.plugin.protobuf.lang.psi.proto.ProtobufElement"
  extends="io.kanro.idea.plugin.protobuf.lang.psi.proto.ProtobufElementBase"

  elementTypeHolderClass="io.kanro.idea.plugin.protobuf.lang.psi.proto.ProtobufTypes"
  elementTypeClass="io.kanro.idea.plugin.protobuf.lang.psi.proto.ProtobufElementType"

  tokens = [
    SKIPPED_WHITESPACE = "regexp:[ \n\t\r\f]+"

    LINE_COMMENT = "regexp://[^\n]*\n?"
    BLOCK_COMMENT = "regexp:(/\*([^*]|(\*+[^*/]))*(\*+/))"

    IDENTIFIER_LITERAL = "regexp:[a-zA-Z_][a-zA-Z0-9_]*"

    INTEGER_LITERAL = "regexp:(0|[1-9][0-9]*)|(0[xX][0-9a-fA-F]+)|(0[0-7]+)"
    FLOAT_LITERAL = "regexp:(\.[0-9]+|(0|[1-9][0-9]*)\.[0-9]*|(0|[1-9][0-9]*))([eE][-+]?[0-9]+)?"

    STRING_LITERAL = "regexp:('(\\.|[^'\n])*')|(\"(\\.|[^\"\n])*\")"

    ASSIGN = '='
    COMMA = ','
    DOT = '.'
    GT = '>'
    LBRACE = '{'
    LBRACK = '['
    LPAREN = '('
    LT = '<'
    MINUS = '-'
    PLUS = '+'
    RBRACE = '}'
    RBRACK = ']'
    RPAREN = ')'
    SEMI = ';'
    SLASH = '/'
    COLON = ':'

    SYMBOL = "regexp:[!#$%&()*+,-./:;<=>?@\[\\\]^`{|}~]"
  ]
}

File ::= (EditionStatement | SyntaxStatement)? FileElement*

private FileElement ::= ImportStatement | PackageStatement | FileOption | TopLevelDefinition | ExtendDefinition | ';' {
    recoverWhile = FileElementRecovery
}
private FileElementRecovery ::= !(message | enum | service | extend | import | package | option | ';')

private TopLevelDefinition ::= MessageDefinition | EnumDefinition | ServiceDefinition

/* File statements */
EditionStatement ::= edition '=' StringValue ';' {
    pin=1
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"]
}

SyntaxStatement ::= syntax '=' StringValue ';' {
    pin=1
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"]
}

PackageStatement ::= package PackageName ('.' PackageName)* ';' {
    pin=1
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"]
}

PackageName::= IdentifierWithKeyword {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufPackageName"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufPackageNameMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufPackageNameStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

ImportStatement ::= import ImportLabel? StringValue ';' {
    pin=1
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufImportStatementMixin"
}

ImportLabel ::= public | weak {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"]
}

FileOption ::= option OptionAssign ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
}

/* Options */
OptionAssign ::= OptionName '=' Constant {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufOptionAssign"
    ]
    recoverWhile=OptionAssignRecovery
}

private OptionAssignRecovery ::= !(option | ',' | '}' | ']' | ';')

OptionName ::= OptionFieldName ('.' OptionFieldName)* {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"]
}

OptionFieldName ::= SymbolName | '(' ExtensionFieldName ')' {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufFileReferenceContributor"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufOptionFieldNameMixin"
}

/* Message */
MessageDefinition ::= message Identifier MessageBody {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufMessageDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufMessageDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufMessageStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

MessageBody ::= '{' MessageElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}

private MessageElement ::=
    MessageDefinition
    | EnumDefinition
    | ReservedStatement
    | ExtensionStatement
    | ExtendDefinition
    | MessageOption
    | OneofDefinition
    | MapFieldDefinition
    | GroupDefinition
    | FieldDefinition
    | ';' {
    recoverWhile = MessageElementRecovery
}
private MessageElementRecovery ::= !(message | enum | extensions | reserved | extend | option | oneof | FieldLabel | group | map | TypeName | '}' | ';')
MessageOption ::= option OptionAssign ';'  {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
}

/* Field */
FieldDefinition ::= FieldLabel? TypeName Identifier '=' IntegerValue FieldOptionBlock? ';'  {
    pin=2
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufFieldDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufFieldDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufFieldStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

FieldOptionBlock ::= '[' FieldOption (',' FieldOption)* ']' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}

FieldOption ::= OptionAssign {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
    recoverWhile=OptionBlockRecovery
}

private OptionBlockRecovery ::= !(',' | ']')

FieldLabel ::= required | optional | repeated {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"]
}

OneofDefinition ::= oneof Identifier OneofBody {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufOneofDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufOneofDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufOneofStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

OneofBody ::= '{' OneofElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}

private OneofElement ::= OneofOption | GroupDefinition | FieldDefinition | ';' {recoverWhile=OneofElementRecovery}
private OneofElementRecovery ::= !(FieldLabel | option | group | TypeName | ';' | '}')
OneofOption ::= option OptionAssign ';'  {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
}

MapFieldDefinition ::= map '<' TypeName ',' TypeName '>' Identifier '=' IntegerValue FieldOptionBlock? ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufMapFieldDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufMapFieldDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufMapFieldStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

GroupDefinition ::= FieldLabel? group Identifier '=' IntegerValue MessageBody {
    pin=2
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufGroupDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufGroupDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufGroupStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

ExtensionStatement ::= extensions ExtensionRanges ';' {
    pin = 1;
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
    ]
}

private ExtensionRanges ::= ExtensionRange (',' ExtensionRange)*

ExtensionRange ::= IntegerValue (to (IntegerValue | max))? ExtensionRangeOptionBlock? {
    recoverWhile=RangeRecovery
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
    ]
}

ExtensionRangeOptionBlock ::= '[' ExtensionRangeOption (',' ExtensionRangeOption)* ']' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}
ExtensionRangeOption ::= OptionAssign {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
    recoverWhile=OptionBlockRecovery
}

ReservedStatement ::= reserved ReservedElement ';'{
    pin = 1;
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
    ]
}

private ReservedElement ::= ReservedRanges | ReservedFieldNames
private ReservedRanges ::= ReservedRange (',' ReservedRange)*
ReservedRange ::= IntegerValue (to (IntegerValue | max))? {
    recoverWhile=RangeRecovery
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
    ]
}

private RangeRecovery ::= !(IntegerValue | ',' | '[' | ';') MessageElementRecovery
private ReservedFieldNames ::= ReservedName  (',' ReservedName)*
ReservedName ::= STRING_LITERAL | IdentifierWithKeyword {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
    ]
}

EnumDefinition ::= enum Identifier EnumBody {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufEnumDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufEnumDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufEnumStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

EnumBody ::= '{' EnumElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}
private EnumElement ::= EnumOption | ReservedStatement | EnumValueDefinition | ';' {recoverWhile=EnumElementRecovery}
private EnumElementRecovery ::= !(option | reserved | EnumValueDefinition | ';' | '}')
EnumOption ::= option OptionAssign ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
}

EnumValueDefinition ::= Identifier '=' IntegerValue EnumValueOptionBlock? ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufEnumValueDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufEnumValueDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufEnumValueStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

EnumValueOptionBlock ::= '[' EnumValueOption (',' EnumValueOption)* ']' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}

EnumValueOption ::= OptionAssign {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
    recoverWhile=OptionBlockRecovery
}

ServiceDefinition ::= service Identifier ServiceBody  {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufServiceDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufServiceDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufServiceStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

ServiceBody ::= '{' ServiceElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}

private ServiceElement ::= ServiceOption | RpcDefinition | ';' {recoverWhile=ServiceElementRecovery}
private ServiceElementRecovery ::= !(option | stream | rpc | ';' | '}')
ServiceOption ::= option OptionAssign ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
}

RpcDefinition ::= rpc Identifier RpcIO returns RpcIO ( RpcBody | ';') {
    pin = 1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufRpcDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufRpcDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufRpcStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

RpcIO ::= '(' stream? TypeName ')' {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"]
}

RpcBody ::= '{' RpcElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}

private RpcElement ::= RpcOption | ';' { recoverWhile=RpcElementRecovery }
private RpcElementRecovery ::= !(option | ';' | '}')
RpcOption ::= option OptionAssign ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufOptionHover"
    ]
}

ExtendDefinition ::= extend TypeName ExtendBody  {
   pin=1
   implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.element.ProtobufExtendDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyOwner"
   ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufExtendDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.impl.ProtobufExtendStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.proto.stub.type.ProtobufStubTypes.get'
}

ExtendBody ::=  '{' ExtendElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
    ]
}

private ExtendElement ::= GroupDefinition | FieldDefinition | ';' {recoverWhile=ExtendElementRecovery}
private ExtendElementRecovery ::= !(FieldLabel | group | TypeName | ';' | '}')

Constant ::= StringValue | NumberValue | BooleanValue | MessageValue | EnumValue {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.StatementElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.value.WrappedValue"
    ]
}

TypeName ::= '.'? SymbolName ('.' SymbolName)* {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufFileReferenceContributor"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufSymbolReferenceHost"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufTypeNameMixin"
}

ExtensionFieldName ::= '.'? SymbolName ('.' SymbolName)* {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
    ]
}

SymbolName::= IdentifierWithKeyword {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
    ]
}

EnumValue ::= IdentifierWithKeyword {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.value.EnumValue"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufEnumValueMixin"
}

Identifier::= IdentifierWithKeyword {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
    ]
}

private IdentifierWithKeyword ::= IDENTIFIER_LITERAL | <<parseKeyword>>

StringValue ::= STRING_LITERAL+ {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufFileReferenceContributor"
        "io.kanro.idea.plugin.protobuf.lang.psi.proto.feature.ProtobufSymbolReferenceHost"
        "io.kanro.idea.plugin.protobuf.lang.psi.value.StringValue"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.proto.mixin.ProtobufStringValueMixin"
}

IntegerValue ::= ('-' | '+')? INTEGER_LITERAL{
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.value.IntegerValue"
    ]
}

NumberValue ::= ('-' | '+')? (INTEGER_LITERAL | FLOAT_LITERAL | 'inf' | 'nan'){
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.value.NumberValue"
    ]
}

BooleanValue ::= (false | true) {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.FragmentElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.value.BooleanValue"
    ]
}

MessageValue ::= '{' << parseTextMessage >> '}' {
    pin=1
    recoverWhile = MessageValueRecovery
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.type.BlockElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.feature.BodyElement"
        "io.kanro.idea.plugin.protobuf.lang.psi.value.MessageValue"
    ]
}

private MessageValueRecovery ::= !('}' | <<parseTextFieldName>>)