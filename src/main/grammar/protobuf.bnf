{
  generate=[names="long" psi="yes" tokens="no"]
  parserClass="io.kanro.idea.plugin.protobuf.lang.parser.ProtobufParser"
  parserUtilClass="io.kanro.idea.plugin.protobuf.lang.parser.ProtobufParserUtil"
  parserImports=[
    "static io.kanro.idea.plugin.protobuf.lang.psi.token.ProtobufTokens.*"
  ]
  psiClassPrefix="Protobuf"
  psiImplClassSuffix="Impl"
  psiPackage="io.kanro.idea.plugin.protobuf.lang.psi"
  psiImplPackage="io.kanro.idea.plugin.protobuf.lang.psi.impl"

  implements="io.kanro.idea.plugin.protobuf.lang.psi.primitive.ProtobufElement"
  extends="io.kanro.idea.plugin.protobuf.lang.psi.primitive.ProtobufElementBase"

  elementTypeHolderClass="io.kanro.idea.plugin.protobuf.lang.psi.ProtobufTypes"
  elementTypeClass="io.kanro.idea.plugin.protobuf.lang.psi.ProtobufElementType"

  tokens = [
    SKIPPED_WHITESPACE = "regexp:[ \n\t\r\f]+"

    LINE_COMMENT = "regexp://[^\n]*\n?"
    BLOCK_COMMENT = "regexp:(/\*([^*]|(\*+[^*/]))*(\*+/))"

    IDENTIFIER_LITERAL = "regexp:[a-zA-Z_][a-zA-Z0-9_]*"

    INTEGER_LITERAL = "regexp:(0|[1-9][0-9]*)|(0[xX][0-9a-fA-F]+)|(0[0-7]+)"
    FLOAT_LITERAL = "regexp:(\.[0-9]+|(0|[1-9][0-9]*)\.[0-9]*|(0|[1-9][0-9]*))([eE][-+]?[0-9]+)?"

    STRING_LITERAL = "regexp:('(\\.|[^'\n])*')|(\"(\\.|[^\"\n])*\")"

    ASSIGN = '='
    COMMA = ','
    DOT = '.'
    GT = '>'
    LBRACE = '{'
    LBRACK = '['
    LPAREN = '('
    LT = '<'
    MINUS = '-'
    RBRACE = '}'
    RBRACK = ']'
    RPAREN = ')'
    SEMI = ';'

    SYMBOL = "regexp:[!#$%&()*+,-./:;<=>?@\[\\\]^`{|}~]"
  ]
}

File ::= SyntaxStatement? FileElement*

private FileElement ::= ImportStatement | PackageStatement | FileOption | TopLevelDefinition | ExtendDefinition | ';' {
    recoverWhile = FileElementRecovery
}
private FileElementRecovery ::= !(message | enum | service | extend | import | package | option | ';')

private TopLevelDefinition ::= MessageDefinition | EnumDefinition | ServiceDefinition

/* File statements */
SyntaxStatement ::= syntax '=' StringValue ';' {
    pin=1
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"]
}
PackageStatement ::= package PackageName ('.' PackageName)* ';' {
    pin=1
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"]
}
PackageName::= IdentifierWithKeyword {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufPackageName"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufPackageNameMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufPackageNameStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}
ImportStatement ::= import ImportLabel? StringValue ';' {
    pin=1
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufImportStatementMixin"
}
ImportLabel ::= public | weak {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
}
FileOption ::= option OptionAssign ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionHover"
    ]
}

/* Options */
OptionAssign ::= OptionName '=' Constant {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufOptionAssign"
    ]
    recoverWhile=OptionAssignRecovery
}
private OptionAssignRecovery ::= !(option | ',' | '}' | ']' | ';')

OptionName ::= (BuiltInOptionName | ExtensionOptionName) ('.' FieldName)* {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
}
BuiltInOptionName ::= IdentifierWithKeyword {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufBuiltInOptionMixin"
}
ExtensionOptionName ::= '(' TypeName ')' {
    pin=1
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
}

/* Message */
MessageDefinition ::= message Identifier MessageBody {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufMessageDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufMessageDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufMessageStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}
MessageBody ::= '{' MessageElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
private MessageElement ::=
    MessageDefinition
    | EnumDefinition
    | ReservedStatement
    | ExtensionStatement
    | ExtendDefinition
    | MessageOption
    | OneofDefinition
    | MapFieldDefinition
    | GroupDefinition
    | FieldDefinition
    | ';' {
    recoverWhile = MessageElementRecovery
}
private MessageElementRecovery ::= !(message | enum | extensions | reserved | extend | option | oneof | FieldLabel | group | map | TypeName | '}' | ';')
MessageOption ::= option OptionAssign ';'  {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionHover"
    ]
}

/* Field */
FieldDefinition ::= FieldLabel? TypeName Identifier '=' IntegerValue FieldOptionBlock? ';'  {
    pin=2
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufFieldDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufFieldDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufFieldStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}
FieldOptionBlock ::= '[' FieldOption (',' FieldOption)* ']' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
FieldOption ::= OptionAssign {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionHover"
    ]
    recoverWhile=OptionBlockRecovery
}

private OptionBlockRecovery ::= !(',' | ']')

FieldLabel ::= required | optional | repeated {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
}

OneofDefinition ::= oneof Identifier OneofBody {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufOneofDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufOneofDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufOneofStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}
OneofBody ::= '{' OneofElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
private OneofElement ::= OneofOption | GroupDefinition | FieldDefinition | ';' {recoverWhile=OneofElementRecovery}
private OneofElementRecovery ::= !(FieldLabel | option | group | TypeName | ';' | '}')
OneofOption ::= option OptionAssign ';'  {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionHover"
    ]
}

MapFieldDefinition ::= map '<' TypeName ',' TypeName '>' Identifier '=' IntegerValue FieldOptionBlock? ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufMapFieldDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufMapFieldDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufMapFieldStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}

GroupDefinition ::= FieldLabel? group Identifier '=' IntegerValue MessageBody {
    pin=2
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufGroupDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufGroupDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufGroupStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}

ExtensionStatement ::= extensions Ranges ';' {
    pin = 1;
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
    ]
}

ReservedStatement ::= reserved ReservedElement ';'{
    pin = 1;
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
    ]
}
private ReservedElement ::= Ranges | FieldNames
private Ranges ::= ReservedRange (',' ReservedRange)*
ReservedRange ::= IntegerValue (to (IntegerValue | max))? {
    recoverWhile=RangeRecovery
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"
    ]
}
private RangeRecovery ::= !(IntegerValue | ',' | '[' | ';') MessageElementRecovery
private FieldNames ::= ReservedName  (',' ReservedName)*
ReservedName ::= IdentifierWithKeyword {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"
    ]
}

EnumDefinition ::= enum Identifier EnumBody {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufEnumDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufEnumDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufEnumStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}

EnumBody ::= '{' EnumElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
private EnumElement ::= EnumOption | ReservedStatement | EnumValueDefinition | ';' {recoverWhile=EnumElementRecovery}
private EnumElementRecovery ::= !(option | reserved | EnumValueDefinition | ';' | '}')
EnumOption ::= option OptionAssign ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionHover"
    ]
}
EnumValueDefinition ::= Identifier '=' IntegerValue EnumValueOptionBlock? ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufEnumValueDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufEnumValueDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufEnumValueStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}
EnumValueOptionBlock ::= '[' EnumValueOption (',' EnumValueOption)* ']' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
EnumValueOption ::= OptionAssign {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionHover"
    ]
    recoverWhile=OptionBlockRecovery
}

ServiceDefinition ::= service Identifier ServiceBody  {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufServiceDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufServiceDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufServiceStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}

ServiceBody ::= '{' ServiceElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
private ServiceElement ::= ServiceOption | RpcDefinition | ';' {recoverWhile=ServiceElementRecovery}
private ServiceElementRecovery ::= !(option | stream | rpc | ';' | '}')
ServiceOption ::= option OptionAssign ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionHover"
    ]
}

RpcDefinition ::= rpc Identifier RpcIO returns RpcIO ( RpcBody | ';') {
    pin = 1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufRpcDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionOwner"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBodyOwner"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufRpcDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufRpcStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}
RpcIO ::= '(' stream? TypeName ')' {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"]
}
RpcBody ::= '{' RpcElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
private RpcElement ::= RpcOption | ';' { recoverWhile=RpcElementRecovery }
private RpcElementRecovery ::= !(option | ';' | '}')
RpcOption ::= option OptionAssign ';' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufOptionHover"
    ]
}

ExtendDefinition ::= extend TypeName ExtendBody  {
   pin=1
   implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufExtendDefinition"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBodyOwner"
   ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufExtendDefinitionMixin"
    stubClass= "io.kanro.idea.plugin.protobuf.lang.psi.stub.impl.ProtobufExtendStub"
    elementTypeFactory = 'io.kanro.idea.plugin.protobuf.lang.psi.stub.type.ProtobufStubTypes.get'
}

ExtendBody ::=  '{' ExtendElement* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}

private ExtendElement ::= GroupDefinition | FieldDefinition | ';' {recoverWhile=ExtendElementRecovery}
private ExtendElementRecovery ::= !(FieldLabel | group | TypeName | ';' | '}')
Constant ::= StringValue | NumberValue | BooleanValue | MessageValue | EnumValue | ArrayValue {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"]
}

TypeName ::= '.'? SymbolName ('.' SymbolName)* {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.feature.ProtobufFileReferenceContributor"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.feature.ProtobufSymbolReferenceHost"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufTypeNameMixin"
}
SymbolName ::= IdentifierWithKeyword {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
}
FieldName ::= IdentifierWithKeyword {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufFieldNameMixin"
}
EnumValue ::= IdentifierWithKeyword {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufEnumValueMixin"
}
Identifier::= IdentifierWithKeyword {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"
    ]
}

private IdentifierWithKeyword ::= IDENTIFIER_LITERAL | <<parseKeyword>>

StringValue ::= STRING_LITERAL {
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.feature.ProtobufFileReferenceContributor"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.feature.ProtobufSymbolReferenceHost"
    ]
    mixin="io.kanro.idea.plugin.protobuf.lang.psi.mixin.ProtobufStringValueMixin"
}
IntegerValue ::= '-'? INTEGER_LITERAL{
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
}
NumberValue ::= '-'? (INTEGER_LITERAL | FLOAT_LITERAL | 'inf' | 'nan'){
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
}
BooleanValue ::= (false | true) {
    implements=["io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufFragment"]
}
MessageValue ::= '{' FieldAssign* '}' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
ArrayValue ::= '[' Constant (',' Constant)* ']' {
    pin=1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufBlock"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.stratify.ProtobufBody"
    ]
}
FieldAssign ::= FieldName ':' Constant (';' | ',')? {
    pin = 1
    implements=[
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.type.ProtobufStatement"
        "io.kanro.idea.plugin.protobuf.lang.psi.primitive.element.ProtobufFieldAssign"
    ]
}